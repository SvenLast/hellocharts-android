plugins {
    id 'com.android.library'
    id 'maven-publish'
}

//To upload to maven central
//http://zserge.com/blog/gradle-maven-publish.html
//gradle uploadArchives

dependencies {
    //compile fileTree(dir: 'libs', include: '*.jar')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
}


android {
    namespace "lecho.lib.hellocharts"
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
    }

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

}

//Execute "gradle clean jarRelease" to cook jar.
android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals("debug")) return

    def jarTask = tasks.create("jar${name.capitalize()}", Jar) {
        dependsOn variant.javaCompileProvider
        from variant.javaCompileProvider.get().destinationDirectory
        archiveBaseName.set("${project.name}-${name}")
        archiveVersion.set(version)
    }

    artifacts.add('archives', jarTask)
}

tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

publishing {
    publications {
        release(MavenPublication) {
            artifact sourceJar
            afterEvaluate { artifact(tasks.getByName("bundleReleaseAar")) }
            groupId = GROUP_ID
            artifactId = ARTIFACT_ID
            version = VERSION

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    // 避免出现空节点或 artifactId=unspecified 的节点
                    if (it.group != null && (it.name != null && "unspecified" != it.name) && it.version != null) {
                        println "dependency=${it.toString()}"
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', 'implementation')
                    }
                }
            }
            // 添加其他元数据（可选）
            pom {
                name = 'Android Base Library'
                description = 'From Creek'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = ''
                        name = ''
                        email = ''
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "release"
            url = uri(RELEASE_URL)
            credentials {
                username = USER_NAME ?: ""
                password = PASSWORD ?: ""
            }
        }
        maven {
            name = "snapshot"
            url = uri(SNAPSHOT_URL)
            credentials {
                username = USER_NAME ?: ""
                password = PASSWORD ?: ""
            }
        }
    }
}

